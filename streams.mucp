-- Streams.

type Source(A) = nu Q. &{end: 1, more: A * Q}.
type Sink(A)   = mu Q. +{end: bot, more: ~A || Q}.

check x<->y |- x:Source(bot), y:Sink(bot).
check x<->y |- x:Source(1), y:Sink(1).

-- Silly proof that _|_ -@ (1 * 1), by constructing an infinite stream of _|_ and then pealing two
-- off for the 1's.  The good news is that this reduces to the sensible thing.

def Bottoms(y,x) =
    corec x [w:1]
      (y<->w,
       case x {end: w<->x;
               more: w().x[v].(v[].0 | x[].0) }).
check Bottoms(y,x) |- y:bot, x:Source(1).

def Ones(z,x) = rec x.x/more.x(v).z[w].(w<->v | rec x.x/end.x<->z).
check Ones(z,x) |- x:Sink(1), z:1*1.

check new [x:Source(1)] (Bottoms(y,x) | Ones(z,x)) |- y:bot, z:1*1.

-- Encodings of exponentials, for Church numerals.

type Quest(A) = mu P.+{w: bot, c: P || P, d: A}.
type Bang(A)  = nu P.&{w:1, c:P * P, d: A}.
type Church = forall A. (Quest(A * ~A) || (~A || A)).

def Zero(x) = x(A).x(s).x(z).rec s.s/w.s().z<->x.
check Zero(x) |- x:Church.

def Ping(x,y,w) =
    x[1].x[s].(corec s [v:~Quest(bot)]
                 (v<->y,
                  case s {w: rec v.v/w.v().s[].0;
                          c: rec v.v/c.v(u).s[t].(t<->u | s<->v);
                          d: rec v.v/d.s(t).t().s<->v}) |
               x[z].(z[].0 | x().w[].0)).

-- Phil's encodings

def One(x) = x(A).x(s).x(z).rec s.s/d.s[a].(a<->z | s<->x).
check One(x) |- x:Church.

def Two(x) = x(A).x(s).x(z).
             rec s.s/c.s(f).rec f.f/d.f[a].
             (a<->z | rec s.s/d.s[a].(a<->f | s<->x)).
check Two(x) |- x:Church.

-- Rewrite Two to use One.

def Two_(x) = x(A).x(s).x(z).
              rec s.s/c.s(f).rec f.f/d.f[a].
              (a<->z | new [y:Church] (One(y) | y[A].y[w].(w <-> s | y[v].(v<->f | y<->x)))).
check Two_(x) |- x:Church.
check new [x:Church] (Two_(x) | Ping(x,y,w)) |- y:Quest(bot),w:1.

-- Increment, based on above.

def Inc(x,y) = y(A).y(s).y(z).
               rec s.s/c.s(f).rec f.f/d.f[a].
               (a<->z | x[A].x[w].(w<->s | x[v].(v<->f | y<->x))).
check Inc(x,y) |- x:~Church,y:Church.

-- Demonstrate that Inc of One behaves as Two.

check new [y:Church] (new [x:Church] (One(x) | Inc(x,y)) |
                      Ping(y,a,b)) |- a:Quest(bot),b:1.

def Zeroes(y) = corec y [z:1]
                  (z[].0,
                   z().case y {w: y[].0;
                               c: y[w].(w[].0|y[].0);
                               d: Zero(y)}).
check Zeroes(y) |- y:Bang(Church).

def LiftIncr(x,y) = corec y [z:Bang(Church)]
                      (x<->z,
                       case y {w:rec z.z/w.z<->y;
                               c:rec z.z/c.z(w).y[v].(v<->w | y<->z);
                               d:rec z.z/d.Inc(z,y)}).
check LiftIncr(x,y) |- x:Quest(~Church), y:Bang(Church).

def Nats(x) = corec x [y:Bang(Church)]
                (Zeroes(y),
                 case x { end: rec y.y/w.y<->x;
                          more: rec y.y/c.y(z).x[w].(rec z.z/d.z<->w | LiftIncr(y,x)) }).
check Nats(x) |- x:Source(Church).

new [x:Source(Church)]
  (Nats(x) |
   rec x.x/more.x(y).
   rec x.x/more.x(z).
   rec a.a/c.a(b).rec a.a/c.a(c).
   d[e].(e[f].(Ping(y,b,f) | Ping(z,c,e)) |
         rec x.x/more.x(y).rec x.x/end.x().Ping(y,a,d)))
 |- a:Quest(bot),d:(1*1)*1.
